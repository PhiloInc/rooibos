namespace Rooibos
  ' /**
  '  * @module TestSuite
  '  * @description All brs files that include `'@TestSuite` annotations automatically extend the TestSuite.
  '  * The base test suite contains all of the assertions, and utility methods required to writey our tests, as well as being responsible for tracking the state of the tests and groups.
  '  */
  class TestGroup
    'test state
    name = "Unnamed Suite"

    testSuite = invalid
    setupFunctionName = invalid
    tearDownFunctionName = invalid
    beforeEachFunctionName = invalid
    afterEachFunctionName = invalid
    isSolo = false
    isLegacy = false
    isIgnored = false
    stats = invalid
    tests = []

    function new(testSuite, data)
      m.testSuite = testSuite
      m.name = data.name
      m.valid = data.valid
      m.hasFailures = testSuite.hasFailures
      m.isSolo = data.isSolo
      m.isIgnored = data.isIgnored
      m.testsData = data.testCases
      m.isNodeTest = false
      m.nodeName = invalid
      m.setupFunctionName = data.setupFunctionName
      m.tearDownFunctionName = data.tearDownFunctionName
      m.beforeEachFunctionName = data.beforeEachFunctionName
      m.afterEachFunctionName = data.afterEachFunctionName

      m.global = testSuite.global
      m.top = testSuite.top
      m.scene = testSuite.scene
      m.stats = new Stats()
    end function

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ running
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    function run()
      m.runSuiteFunction(m.setupFunctionName, "setup")

      for each testData in m.testsData
        test = new Test(m, testData)
        m.tests.push(test)
        m.runSuiteFunction(m.beforeEachFunctionName, "beforeEach")

        m.testSuite.runTest(test)
        m.stats.appendTestResult(test.result)

        m.runSuiteFunction(m.afterEachFunctionName, "afterEach")

        if m.stats.hasFailures and m.testSuite.isFailingFast
          ? "Terminating group due to failed test"
          exit for
        end if

      end for

      m.runSuiteFunction(m.tearDownFunctionName, "tearDown")
    end function

    private function runSuiteFunction(methodName, defaultMethodName) 
      if methodName <> invalid and methodName <> ""
        m.testSuite[methodName]()
      else
        m.testSuite[defaultMethodName]()
      end if

    end function

  end class
end namespace